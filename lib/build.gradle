ext {
    publishedName = "datatables-ajax"
}
archivesBaseName = publishedName

bintray {
    pkg {
        repo = 'Maven'
        name = publishedName
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Berna-L/datatables-ajax-java.git'
        publications = ['mavenPublication']
        configurations = ['archives']
        version {
            name = project.version
            desc = "DataTables Ajax Library ${project.version}"
            released = new Date()
            vcsTag = project.version
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "Berna-L"
            name "Bernardo Lopes"
            email "berna.leao@gmail.com"
        }
    }

    scm {
        url "https://github.com/Berna-L/datatables-ajax-java"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId publishedName
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java Library for interfacing with DataTables Javascript library via AJAX')
                root.appendNode('name', 'Datatables AJAX')
                root.appendNode('url', 'https://github.com/Berna-L/datatables-ajax-java')
                root.children().last() + pomConfig
            }
        }
    }
}